plugins {
	id 'fabric-loom' version '1.6-SNAPSHOT'
	id 'maven-publish'
	id "com.github.johnrengelman.shadow" version "8.1.1"
}

apply plugin: 'java'
apply plugin: 'fabric-loom'
apply plugin: 'maven-publish'
apply plugin: "com.github.johnrengelman.shadow"

defaultTasks('clean', 'build', 'publishToMavenLocal')

version = project.mod_version
group = project.maven_group

repositories {
	mavenLocal()
}

configurations {
	shade
	compileClasspath.extendsFrom shade
}

dependencies {
	// To change the versions see the gradle.properties file
	minecraft "com.mojang:minecraft:${project.minecraft_version}"
	mappings "net.fabricmc:yarn:${project.yarn_mappings}:v2"
	modImplementation "net.fabricmc:fabric-loader:${project.loader_version}"

	// Fabric API. This is technically optional, but you probably want it anyway.
	modImplementation "net.fabricmc.fabric-api:fabric-api:${project.fabric_version}"

	// Uncomment the following line to enable the deprecated Fabric API modules.
	// These are included in the Fabric API production distribution and allow you to update your mod to the latest modules at a later more convenient time.

	// modImplementation "net.fabricmc.fabric-api:fabric-api-deprecated:${project.fabric_version}"
	api 'de.iani.cubeside:CubesideUtilsCore:1.16-SNAPSHOT'
	implementation 'com.mysql:mysql-connector-j:8.0.33'
}

processResources {
	inputs.property "version", project.version

	filesMatching("fabric.mod.json") {
		expand "version": project.version
	}
}

loom {
    accessWidenerPath = file("src/main/resources/cubesideutils.accesswidener")
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
	it.options.release = 21
}

java {
	// Loom will automatically attach sourcesJar to a RemapSourcesJar task and to the "build" task
	// if it is present.
	// If you remove this line, sources will not be generated.
	withSourcesJar()

	sourceCompatibility = JavaVersion.VERSION_21
	targetCompatibility = JavaVersion.VERSION_21
}

jar {
	from("LICENSE") {
		rename { "${it}_${project.base.archivesName.get()}"}
	}
}

shadowJar {
    // archiveBaseName.set('cubesideutilsforge')
    archiveClassifier.set('shaded')

    dependencies {
        include(dependency('de.iani.cubeside:CubesideUtilsCore'))
        include(dependency('com.mysql:mysql-connector-j'))
        exclude "META-INF/services/java.sql.Driver"
    }
    
    relocate 'com.mysql', 'de.iani.cubesideutils.fabric.libs.mysql'
}

remapJar {
    dependsOn(shadowJar)
    mustRunAfter(shadowJar)
    inputFile = shadowJar.archivePath
}

// configure the maven publication
publishing {
	publications {
		mavenJava(MavenPublication) {
			from components.java
		}
	}

	// See https://docs.gradle.org/current/userguide/publishing_maven.html for information on how to set up publishing.
	repositories {
		// Add repositories to publish to here.
		// Notice: This block does NOT have the same function as the block in the top level.
		// The repositories here will be used for publishing your artifact, not for
		// retrieving dependencies.
	}
}
